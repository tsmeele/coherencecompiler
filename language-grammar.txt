<Program> ::= <ParallelStatement> | <Protocol>
<ParallelStatement> ::= TEXT_PARALLEL <Protocol> <ParallelAndClause>
<Protocol> ::= TEXT_PROTOCOL <ProtocolName> <Participants> <StatementBlock>  + f:PROTOCOL()
<ProtocolName> ::= IDENTIFIER -
<Participants> ::= BRACKETOPEN <Role> [<MoreRoles>] BRACKETCLOSE  + f:PARTICIPANTS()
<Role> ::= <RoleName> [<BracketedIrodsAttribute>]  + f:ROLE()
<RoleName> ::= IDENTIFIER -
<BracketedIrodsAttribute> ::= BRACKETOPEN <IrodsAttribute> BRACKETCLOSE
<IrodsAttribute> ::= IDENTIFIER -
<MoreRoles> ::= COMMA <Role> [<MoreRoles>]
<StatementBlock> ::= CURLYOPEN <Statements> CURLYCLOSE  + f:STATEMENT_BLOCK()
<Statements> ::= <CommunicationStatement> <Statements> | <CoherentStatement> <Statements> | <AtomicStatement> <Statements> | - | <EmptyStatement> <Statements>
<CommunicationStatement> ::= <FromRole> <ToRole> [<WithClause>] <IrodsOperation> SEMICOLON  + f:COMMUNICATION()
<FromRole> ::= TEXT_FROM <Role>
<ToRole> ::= TEXT_TO <Role>
<WithClause> ::= TEXT_WITH <IntegerExpression> TEXT_THREADS  + f:WITH_THREADS()
<IntegerExpression> ::= <IntegerOperand> [<IntegerOperations>]
<IntegerOperand> ::= <IntegerOperandBracketed> | <IntegerOperandLiteral> | <IntegerOperandMinus>
<IntegerOperandBracketed> ::= BRACKETOPEN <IntegerExpression> BRACKETCLOSE
<IntegerOperandLiteral> ::= INTEGERLITERAL -
<IntegerOperandMinus> ::= MINUS <IntegerExpression>
<IntegerOperations> ::= <IntegerAddition> [<IntegerOperations>] | <IntegerSubtraction> [<IntegerOperations>]
<IntegerAddition> ::= PLUS <IntegerOperand>  + f:INTEGER_ADDITION()
<IntegerSubtraction> ::= MINUS <IntegerOperand>  + f:INTEGER_SUBTRACTION()
<IrodsOperation> ::= IDENTIFIER -
<CoherentStatement> ::= TEXT_COHERENT <Role> [<MoreRoles>] SEMICOLON  + f:COHERENT()
<AtomicStatement> ::= TEXT_ATOMIC <IrodsOperation> <FromRole> <ProtectsRole> <AtomicBlock>  + f:ATOMIC_STATEMENT()
<ProtectsRole> ::= TEXT_PROTECTS <Role>
<AtomicBlock> ::= <StatementBlock>  + f:ATOMIC_BLOCK()
<EmptyStatement> ::= SEMICOLON -
<ParallelAndClause> ::= TEXT_AND <Protocol> SEMICOLON
